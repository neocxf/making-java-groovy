apply plugin:'groovy'
apply plugin:'eclipse'

repositories {
    mavenCentral()
    mavenRepo urls:["http://download.java.net/maven/1",
        "http://download.java.net/maven/2"]
}

configurations {
    jaxws
}

sourceSets {
    main {
        java { srcDirs = [] }
        groovy { srcDir 'src' }
    }
    test {
        java { srcDirs = [] }
        groovy { srcDir 'test' }
        resources { srcDir 'resources' }
    }
}

task wsgen(dependsOn: compileGroovy)  {
    doLast{
        ant {
            taskdef(name:'wsgen',
                classname:'com.sun.tools.ws.ant.WsGen',
                classpath:configurations.jaxws.asPath)
            wsgen(keep:true,
                destdir: 'bin',
                sourcedestdir:'src',
                resourcedestdir:'resources',
                genwsdl:'true',
                classpath:sourceSets.main.classes.asPath,
                sei:'mjg.calc.service.Calculator')
        }
    }
}
wsgen.onlyIf { !(new File('src/mjg/calc/service')).exists() }
classes.dependsOn wsgen

task wrapper(type:Wrapper) {
    gradleVersion = '1.0'
}

def groovyVersion = '2.1.6'
def spockVersion = '0.7-groovy-2.0'

dependencies {
    compile "org.codehaus.groovy:groovy-all:$groovyVersion"
    jaxws 'com.sun.xml.ws:jaxws-tools:2.1.4'

    testCompile "org.spockframework:spock-core:$spockVersion"
}
